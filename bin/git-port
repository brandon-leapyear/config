#!/usr/bin/env zsh
#
# Starts a branch that ports the given release branch.

set -eu -o pipefail

usage="
Usage: git port X.Y
"

function abort { print "$@" >&2; exit 1 }
function abort_help { abort -f '%s\n' "$@" $usage }

function remote_branch_exists {
    git rev-parse "origin/${1}" &> /dev/null
}

function git_port {
    if [[ $# != 1 ]]; then
        abort_help
    elif [[ -n "$(git ls-files -m --exclude-standard -o)" ]]; then
        abort "Modified or untracked files found"
    fi

    local release_branch=$1

    git fetch origin -q

    local port_branch="port-${release_branch}"

    if remote_branch_exists $port_branch; then
        abort "${port_branch} already exists upstream"
    elif ! remote_branch_exists $release_branch; then
        abort "${release_branch} doesn't exist upstream"
    fi

    git checkout -b $port_branch
    git reset --hard "origin/${release_branch}"
}

git_port "$@"
