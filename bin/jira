#!/usr/bin/env zsh
#
# Open the URL for a JIRA issue. Can be used in two ways:
#   - `jira` opens the TAG copied on the clipboard
#   - `jira TAG` opens the given TAG
#
# TAG can be either a full designation (e.g. LY-100) or
# just the number (defaults to "LY-#").

set -eu -o pipefail

usage="
Usage: $0 [TAG]

Examples:
jira LY-100
jira 100     # prepends LY if none
jira         # uses tag from clipboard
"

function abort { print "$@" >&2; exit 1 }
function abort_help { abort -f '%s\n' "$@" $usage }

function jira {
    local args=()
    for arg in "$@"; do
        case $arg in
            (-h|--help)
                print $usage
                exit 0
            ;;
            (*)
                args+=$arg
            ;;
        esac
    done

    local tag=${args[1]:-$(pbpaste)}

    # Check if tag is numeric; prepend "LY"
    if [[ $tag =~ ^[0-9]+$ ]]; then
        tag="LY-${tag}"
    fi

    # strip whitespace
    tag=$(print $tag | sed 's/ *$//')

    local cleaned_tag=$(echo $tag | tr '[a-z]' '[A-Z]')
    if [[ ! $cleaned_tag =~ ^[A-Z]+-[0-9]+$ ]]; then
        abort_help "Invalid tag: ${tag}"
    fi

    open "http://leapyear.atlassian.net/browse/${cleaned_tag}"
}

jira "$@"
